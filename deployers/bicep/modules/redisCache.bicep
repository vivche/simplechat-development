targetScope = 'resourceGroup'

param location string
param appName string
param environment string

@description('Authentication type for Azure Cache for Redis.')
@allowed([
  'key'
  'managed_identity'
])
param redisAuthenticationType string = 'managed_identity'

param tags object

param enableDiagLogging bool
param logAnalyticsId string

// Import diagnostic settings configurations
module diagnosticConfigs 'diagnosticSettings.bicep' = if (enableDiagLogging) {
  name: 'diagnosticConfigs'
}

var redisConfiguration = redisAuthenticationType == 'managed_identity' ? {
  'aad-enabled': 'true'
} : {}

// deploy redis cache if required
resource redisCache 'Microsoft.Cache/Redis@2024-11-01' = {
  name: toLower('${appName}-${environment}-redis')
  location: location
  properties: {
    sku: {
      name: 'Standard'
      family: 'C'
      capacity: 0
    }
    enableNonSslPort: false
    minimumTlsVersion: '1.2'
    redisConfiguration: redisConfiguration
  }
  tags: tags
}

// configure diagnostic settings for redis cache
resource redisCacheDiagnostics 'Microsoft.Insights/diagnosticSettings@2021-05-01-preview' = if (enableDiagLogging) {
  name: toLower('${redisCache.name}-diagnostics')
  scope: redisCache
  properties: {
    workspaceId: logAnalyticsId
    #disable-next-line BCP318 // expect one value to be null
    logs: diagnosticConfigs.outputs.standardLogCategories
    #disable-next-line BCP318 // expect one value to be null
    metrics: diagnosticConfigs.outputs.standardMetricsCategories
  }
}

output redisCacheName string = redisCache.name
output redisCacheHostName string = redisCache.properties.hostName
